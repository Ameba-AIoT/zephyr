# SPDX-License-Identifier: Apache-2.0

if (NOT ZEPHYR_SDK_INSTALL_DIR)
  message(FATAL_ERROR "ZEPHYR_SDK_INSTALL_DIR must be set")
endif()

if(DEFINED ENV{RSDK_WRAPPER})
  set(RSDK_WRAPPER $ENV{RSDK_WRAPPER})
endif()

set(TOOLCHAIN_HOME ${ZEPHYR_SDK_INSTALL_DIR})

set(COMPILER gcc)
set(LINKER ld)
set(BINTOOLS gnu)

set(CROSS_COMPILE_TARGET_arm      asdk-elf)
set(CROSS_COMPILE_TARGET_riscv    vsdk-elf)
set(CROSS_COMPILE_TARGET_mips     mips-elf)

set(CROSS_COMPILE_TARGET ${CROSS_COMPILE_TARGET_${ARCH}})
set(SYSROOT_TARGET       ${CROSS_COMPILE_TARGET})

if(NOT DEFINED RSDK_WRAPPER)
  message(WARNING "RSDK_WRAPPER is not defined. The default name is ${CROSS_COMPILE_TARGET}")
  set(RSDK_WRAPPER ${CROSS_COMPILE_TARGET})
endif()

find_path(RSDK_HOME NAMES bin/${CROSS_COMPILE_TARGET}-gcc
  PATHS ${TOOLCHAIN_HOME}/${RSDK_WRAPPER}
  PATH_SUFFIXES linux/newlib)

if(${RSDK_HOME} STREQUAL RSDK_HOME-NOTFOUND)
  message(FATAL_ERROR "Unable find ${CROSS_COMPILE_TARGET}-gcc in either path: \
${TOOLCHAIN_HOME}/${RSDK_WRAPPER} or ${TOOLCHAIN_HOME}/${CROSS_COMPILE_TARGET}")
endif()

set(CROSS_COMPILE ${RSDK_HOME}/bin/${CROSS_COMPILE_TARGET}-)
set(SYSROOT_DIR   ${TOOLCHAIN_HOME}/${SYSROOT_TARGET}/${SYSROOT_TARGET})
set(TOOLCHAIN_HAS_NEWLIB ON CACHE BOOL "True if toolchain supports newlib")

